# Использование Map

Теперь смоделируем те же сущности с помощью словарей:

```
defmodule MyCalendar.Model.Map do

  defmodule Place do

    def new(office, room) do
      %{
        office: office,
        room: room
      }
    end

  end

  defmodule Participant do

    def new(name, role) do
      %{
        name: name,
        role: role
      }
    end

  end

  defmodule Topic do

    def new(subject, description) do
      %{
        subject: subject,
        description: description
      }
    end

  end

  defmodule Event do

    def new(title, place, time, participants, agenda) do
      %{
        title: title,
        place: place,
        time: time,
        participants: participants,
        agenda: agenda
      }
    end

  end

end
```

Создадим встречу:
```
defmodule MyCalendar do

  ...

  def sample_event_map() do
    alias MyCalendar.Model.Map, as: M

    place = M.Place.new("Office #1", "Room 123")
    time = ~U[2024-07-05 15:00:00Z]
    participants = [
      M.Participant.new("Kate", :project_manager),
      M.Participant.new("Bob", :developer),
      M.Participant.new("Bill", :qa),
    ]
    agenda = [
      M.Topic.new("Release MyCalendar 1.0", "disscuss release"),
      M.Topic.new("Buy cookies", "disscuss cookies")
    ]
    M.Event.new("Team Meeting", place, time, participants, agenda)
  end
end
```

И запустим:
```
$ iex -S mix
iex(1)> event = MyCalendar.sample_event_map
%{
  time: ~U[2024-07-05 15:00:00Z],
  title: "Team Meeting",
  participants: [
    %{name: "Kate", role: :project_manager},
    %{name: "Bob", role: :developer},
    %{name: "Bill", role: :qa}
  ],
  place: %{office: "Office #1", room: "Room 123"},
  agenda: [
    %{description: "disscuss release", subject: "Release MyCalendar 1.0"},
    %{description: "disscuss cookies", subject: "Buy cookies"}
  ]
}
```

Посмотрим на некоторые продвинутые способы работы со словарями.


## Map.update/4

Полезная функция, но с довольно сложными аргументами, с которыми нужно разобраться.

```elixir
update(
  map(),
  key(),
  default :: value(),
  (existing_value :: value() -> updated_value :: value())
) :: map()
```

Проще всего сделать это на конкретном примере.

Допустим, мы хотим добавить в уже готовое событие еще одного участника.

```elixir-iex
iex(2)> alias MyCalendar.Model.Map, as: M
iex(3)> helen = M.Participant.new("Helen", :developer)
%{name: "Helen", role: :developer}
```

Можно пойти сложным путём, по-очереди обновляя вложенные ключи словаря:

```elixir-iex
> participants = [helen | event.participants]
> event = %{event | participants: participants}
```

Или можно вызывать функцию `Map.update/4`:

```elixir-iex
> john = M.Participant.new("John", :ba)
> Map.update(event, :participants, [], fn p -> [john | p] end)
```

Первый аргумент -- словарь, второй -- ключ, значение которого нужно обновить. Четвертый аргумент -- анонимная функция, которая принимает старое значение ключа, и должна вернуть новое значение ключа.

Тут есть еще третий аргумент, который используется как значение по умолчанию, если данного ключа нет в map. Тогда ключ добавляется с этим значением, а анонимная функция не вызывается.

Есть еще функция **update!** с тремя аргументами, которая бросает исключение в случае отсутствия ключа, и значение по умолчанию ей не нужно.

```elixir-iex
> maria = M.Participant.new("Maria", :hr)
> Map.update!(event, :participants, fn p -> [maria | p] end)
```

Ещё пример:
```
 event = Map.update(event, :time, nil, fn dt -> DateTime.add(dt, 7, :day) end)
 ```

Мы можем добавить такую функцию в модуль Event:

```
  defmodule Event do

    ...

    def add_participant(event, participant) do
      Map.update!(event, :participants, fn p -> [participant | p] end)
    end

  end
```

```
> recompile
> M.Event.add_participant(event, john)
```

## get_in, put_in, update_in

В 4-м уроке я упоминал набор функций и макросов `get_in`, `put_in`, `update_in` для работы с вложнными словарями. Сейчас познакомимся с ними подробнее.

**get_in**

```
> event = MyCalendar.sample_event_map()
> get_in(event, [:place, :room])
"Room 123"
```

`get_in` имеет только форму функции, потому что чтение доступно и так, без макроса.

```
> event.place.room
"Room 123"
```

Ситуация немного сложнее, если у нас есть вложенный список. Тогда нам понадобится указать функцию, которая решит, что делать с этим списком.

Специально для этого есть набор функций в модуле `Access`, так что необходимость писать свою функцию возникает редко:

```
get_in(event, [:agenda, Access.all(), :subject])
```

`Access.all` обходит весь список, и возвращает все значения, которые получаются таким образом.

Давайте посмотрим ещё более сложную структуру с двумя вложенными списками:

```
%{
  list1: [
    %{a: 1, list2: [%{b: 100}, %{b: 200}]},
    %{a: 2, list2: [%{b: 300}, %{b: 400}]},
    %{a: 3, list2: [%{b: 500}, %{b: 600}]}
  ]
}
iex(6)> get_in(data, [:list1, Access.all(), :list2, Access.all(), :b])
[[100, 200], [300, 400], [500, 600]]
```

Если мы хотим взять какой-то один элемент списка, то можем использовать функцию `Access.at`:

```
get_in(event, [:agenda, Access.at(0), :subject])
get_in(event, [:agenda, Access.at(1), :subject])
```

Или мы можем выбрать элементы списка по предикату:
```
get_in(event, [:participants, Access.filter(fn p -> p.role == :developer end), :name])
```

**put_in**

Читать элементы вложенных структур не так сложно. Для этого можно использовать сопоставление с образцом и модуль Enum. А вот изменять значения внутри вложенных структур было бы очень неудобно, если бы не было `put_in`.

Мы уже видели такой пример с функцией `Map.update/4`, вот ещё один пример.

Допустим, мы хотим поменять комнату. Это можно сделать так:
```
iex(2)> place = event.place
iex(3)> new_place = %{place | room: "Room 456"}
iex(4)> event = %{event | place: new_place}
```

Или это можно сделать и в одну строку:
```
event = %{event | place: %{event.place | room: "Room 789"}}
```
но такой код трудно читать. А ведь вложенность может быть и глубже. И это мы ещё не пробовали что-то менять внутри вложенных списков.

С помощью `put_in` это можно сделать так:
```
event = put_in(event, [:place, :room], "Room 123")
```

Или если путь не вычисляется динамически, то можно использовать макрос:
```
event = put_in(event.place.room, "Room 456")
```

Макрос разворачивается в одноименную функцию.

Попробуем изменить агенду. Здесь путь будет динамический, поэтому используем функцию:
```
event = put_in(event, [:agenda, Access.at(0), :subject], "Release 2.0")
```

**update_in**

Функция и макрос `update_in` удобны, когда нужно не просто задать какое-то новое значение, а вычислить его на основе старого:

```
event = update_in(event.place.office, fn office -> String.upcase(office) end)
```

Здесь мы использовали макрос. А для обновления имен участников используем функцию:
```
event = update_in(event, [:participants, Access.all(), :name], fn name -> String.upcase(name) end)
```

Модуль `Access` содержит функции для обхода структур, состоящих из Map, Keyword List, Tuple. Это гибкий инструмент для работы с вложенными структурами данных.

